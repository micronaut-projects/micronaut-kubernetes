plugins {
    id 'io.micronaut.build.internal.kubernetes-examples'
    id 'groovy'
}

micronaut {
    version '4.0.0-SNAPSHOT'
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("micronaut.operator.*")
    }
}

dependencies {
    annotationProcessor(mnValidation.micronaut.validation.processor)
    annotationProcessor(mnSerde.micronaut.serde.processor)
    implementation(mnSerde.micronaut.serde.jackson)
    implementation(mnValidation.micronaut.validation)
    implementation(mn.micronaut.runtime)
    implementation(mn.micronaut.http.client)
    implementation(mn.micronaut.management)
    implementation projects.kubernetesOperator

    runtimeOnly(mnLogging.logback.classic)

    testImplementation projects.testUtils
}

application {
    mainClass.set("micronaut.operator.Application")
}

tasks {
    dockerBuild {
        images = ['micronaut-kubernetes-operator-example']
    }

    dockerBuildNative {
        images = ['micronaut-kubernetes-operator-example']
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module("io.micronaut:micronaut-jackson-databind"))
                .using(module("io.micronaut.serde:micronaut-serde-jackson:2.0.0-M1"))
    }
}