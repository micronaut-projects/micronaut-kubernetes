plugins {
    id 'io.micronaut.build.internal.kubernetes-examples'
    id 'groovy'
}

micronaut {
    version '4.0.0-SNAPSHOT'
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("micronaut.service.*")
    }
}

dependencies {
    annotationProcessor(mnValidation.micronaut.validation.processor)
    annotationProcessor(mnSerde.micronaut.serde.processor)
    implementation(mnValidation.micronaut.validation)
    implementation(mn.micronaut.runtime)
    implementation(mn.micronaut.http.client)
    implementation(mnSerde.micronaut.serde.jackson)
    implementation(mn.micronaut.management)
    implementation projects.kubernetesClient

    runtimeOnly(mnLogging.logback.classic)
}

application {
    mainClass.set("micronaut.service.Application")
}
tasks {
    dockerBuild {
        images = ['micronaut-kubernetes-client-example']
    }

    dockerBuildNative {
        images = ['micronaut-kubernetes-client-example']
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module("io.micronaut:micronaut-jackson-databind"))
                .using(module("io.micronaut.serde:micronaut-serde-jackson:2.0.0-M1"))
    }
}
