import org.gradle.nativeplatform.platform.OperatingSystem
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id "com.google.cloud.tools.jib" version "1.7.0"
    id "java"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "net.ltgt.apt-idea" version "0.21"
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly 
}

dependencies {
    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation 'io.micronaut:micronaut-management'

    implementation project(':kubernetes-discovery-client')

    runtime "ch.qos.logback:logback-classic:1.2.3"
    testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation "io.micronaut.test:micronaut-test-junit5:$micronautTestVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

shadowJar {
    mergeServiceFiles()
}

test.classpath += configurations.developmentOnly

mainClassName = "micronaut.service.Application"
// use JUnit 5 platform
tasks.withType(Test) {
    useJUnitPlatform()
}

OperatingSystem os = DefaultNativePlatform.currentOperatingSystem

jib {
    from {
//        image = 'gcr.io/distroless/java:debug'
        image = 'alvarosanchez/alpine-with-curl'
    }
    to {
        image = 'micronaut-kubernetes-example-service'
        if (os.isMacOsX()) {
            credHelper = 'osxkeychain'
        }
    }
}
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
}
